plugins {
    id 'java-library'
    id "io.freefair.lombok" version "5.0.0-rc6"
    id "com.github.ben-manes.versions" version "0.28.0"
    id 'org.ajoberstar.reckon' version '0.12.0'
    id "com.jfrog.bintray" version "1.8.5"
    id 'maven-publish'
}

group 'com.robbypond'

repositories {
    jcenter()
}

dependencies {
    api 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-moshi:2.8.1'
    implementation 'com.squareup.okhttp3:okhttp:4.5.0'
    implementation 'com.googlecode.jatl:jatl:0.2.3'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'org.slf4j:slf4j-api:1.7.30'

    testCompile group: 'junit', name: 'junit', version: '4.13'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

reckon {
    scopeFromProp()
    stageFromProp('milestone', 'rc', 'final')
    // alternative to stageFromProp
    // snapshotFromProp()
}

reckonTagCreate.dependsOn check

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "robbypond"
            name "Robby Pond"
            email "robert.w.pond@gmail.com"
        }
    }

    scm {
        url "https://github.com/robbypond/mercury-api-client"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A client for the Mercury Parser API')
                root.appendNode('name', project.name)
                root.appendNode('url', 'https://github.com/robbypond/mercury-api-client')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = project.name
        name = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/robbypond/mercury-api-client.git'
        version {
            name = project.version
            desc = project.version
            released  = new Date()
        }
    }
    publish = true
}